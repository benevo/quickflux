QT += quick

TEMPLATE = lib

CONFIG += plugin

TARGET = $$qtLibraryTarget(QuickFlux)
uri = QuickFlux
API_VER=1.0

MOC_DIR = .moc
OBJECTS_DIR = .obj

# Input
SOURCES += \
    quickflux_plugin.cpp

HEADERS += \
    quickflux_plugin.h

include(quickflux.pri)

!equals(_PRO_FILE_PWD_, $$OUT_PWD) {
    copy_qmldir.target = $$OUT_PWD/qmldir
    copy_qmldir.depends = $$_PRO_FILE_PWD_/qmldir
    copy_qmldir.commands = $(COPY_FILE) \"$$replace(copy_qmldir.depends, /, $$QMAKE_DIR_SEP)\" \"$$replace(copy_qmldir.target, /, $$QMAKE_DIR_SEP)\"
    QMAKE_EXTRA_TARGETS += copy_qmldir
    PRE_TARGETDEPS += $$copy_qmldir.target
}

qmldir.files = qmldir
unix {
    installPath = $$[QT_INSTALL_QML]/$$replace(uri, \\., /)
    qmldir.path = $$installPath
    target.path = $$installPath
    INSTALLS += target qmldir
}
